# Configure template for RCS
#	$Id: configure.in,v 1.2 1995/06/16 06:19:24 eggert Exp $
# Copyright 1995 Paul Eggert
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.68)
AC_INIT([RCS], 5.7, [rcs-bugs@cs.purdue.edu])
AC_CONFIG_SRCDIR([src/rcs.c])
AC_LANG([C])

AC_CONFIG_HEADER(config.h:config.hin)
AM_INIT_AUTOMAKE([1.11.1 no-define])

AC_DEFUN([RCS_CBOOL],[dnl
dnl $1 -- C variable (actually #define) name
dnl $2 -- sense that maps to "1" (yes, no, set, etc.)
dnl $3 -- shell variable name to check
dnl $4 -- description for the config header file
AS_IF([test $2 = $[]$3],[cbool=1],[cbool=0])
AC_DEFINE_UNQUOTED([$1],[$cbool],[$4])dnl
])dnl

AC_ISC_POSIX
AC_MINIX
AC_C_CONST
AC_C_PROTOTYPES
AC_HEADER_STDC
AC_CHECK_HEADERS_ONCE([limits.h mach/mach.h net/error.h

pwd.h signal.h siginfo.h stdarg.h varargs.h utime.h unistd.h fcntl.h stdlib.h string.h sys/file.h sys/mman.h sys/wait.h
ucontext.h time.h sys/param.h])
AC_HEADER_DIRENT
AC_HEADER_STAT
AC_HEADER_SYS_WAIT

# types
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SSIZE_T
AC_TYPE_UID_T
AC_TYPE_SIGNAL
AC_CHECK_FUNCS_ONCE(fchmod ftruncate getpwuid_r psiginfo dup2 memchr sigaction strchr strerror tmpnam)
AC_CHECK_FUNCS_ONCE(getuid setuid setgid)
AC_CHECK_FUNCS_ONCE(fputs vfprintf _doprintf _doprnt)
AC_CHECK_FUNCS(madvise map_fd)
AC_CHECK_FUNCS(readlink mktemp rename memmove)
AC_CHECK_FUNCS(psignal kill)
AC_REPLACE_FUNCS(memchr waitpid)

AC_CHECK_MEMBERS([struct sigaction.sa_sigaction], , , [[#include <signal.h>]])

# funcs
AC_FUNC_ALLOCA
AC_FUNC_CLOSEDIR_VOID
AC_FUNC_VFORK
AC_FUNC_VPRINTF
AC_FUNC_MMAP
AC_STRUCT_ST_BLKSIZE

# Set up simple `diff' test.
echo 0 >conftest0
echo 0 >conftest0c
echo 1 >conftest1
cat >conftestok <<'EOF'
d1 1
a1 1
1
EOF

AC_ARG_WITH(diffutils,
  [  --with-diffutils        assume GNU diffutils is similarly installed],
  [with_diffutils=$withval],
  [with_diffutils=no]
)

case $with_diffutils in
yes)
  : ${DIFF='$(bindir)/diff'}
  : ${DIFF3=${DIFF}3}
  : ${DIFF3_BIN=1}
  : ${DIFFFLAGS=-an}
  : ${DIFF_FAILURE=1}
  : ${DIFF_L=1}
  : ${DIFF_SUCCESS=0}
  : ${DIFF_TROUBLE=2}
esac

# Set DIFF to the name of the `diff' program to be run.
# On some systems, the RCS-compatible diff program is called `rdiff';
# use it if it works and `diff' doesn't.
AC_SUBST(DIFF)
AC_MSG_CHECKING([diff basename])
AC_CACHE_VAL(rcs_cv_prog_diff, [
  rcs_cv_prog_diff=$DIFF
  case $rcs_cv_prog_diff in
  '')
    for i in diff /usr/lib/rdiff rdiff
    do
      sh -c "exec $i -n conftest0 conftest1" >conftestout 2>/dev/null
      case $? in
      1)
	if cmp -s conftestok conftestout
	then rcs_cv_prog_diff=$i; break
	fi
	;;
      esac
    done
    ;;
  esac
])
DIFF=$rcs_cv_prog_diff
case $DIFF in
'') AC_MSG_ERROR(cannot find RCS-compatible diff);;
esac
AC_MSG_RESULT($DIFF)
AC_PATH_PROG(DIFF, $DIFF, $DIFF)

# Set DIFF_SUCCESS, DIFF_FAILURE, DIFF_TROUBLE to diff's exit status
# when it finds no differences, some differences, or trouble.
AC_SUBST(DIFF_SUCCESS)
AC_MSG_CHECKING([diff success status])
AC_CACHE_VAL(rcs_cv_status_diff_success, [
  rcs_cv_status_diff_success=$DIFF_SUCCESS
  case $rcs_cv_status_diff_success in
  '')
    # We can't use `$DIFF conftest0 conftest0',
    # since buggy NEXTSTEP 3.0 diff silently yields exit status 2 for this.
    $DIFF conftest0 conftest0c >/dev/null 2>&1
    rcs_cv_status_diff_success=$?
    ;;
  esac
])
DIFF_SUCCESS=$rcs_cv_status_diff_success
AC_MSG_RESULT($DIFF_SUCCESS)
#
AC_SUBST(DIFF_FAILURE)
AC_MSG_CHECKING([diff failure status])
AC_CACHE_VAL(rcs_cv_status_diff_failure, [
  rcs_cv_status_diff_failure=$DIFF_FAILURE
  case $rcs_cv_status_diff_failure in
  '')
    $DIFF conftest0 conftest1 >/dev/null 2>&1
    rcs_cv_status_diff_failure=$?
    ;;
  esac
])
DIFF_FAILURE=$rcs_cv_status_diff_failure
AC_MSG_RESULT($DIFF_FAILURE)
#
AC_SUBST(DIFF_TROUBLE)
AC_MSG_CHECKING([diff trouble status])
AC_CACHE_VAL(rcs_cv_status_diff_trouble, [
  rcs_cv_status_diff_trouble=$DIFF_TROUBLE
  case $rcs_cv_status_diff_trouble in
  '')
    $DIFF conftest0 no/such/file >/dev/null 2>&1
    rcs_cv_status_diff_trouble=$?
    ;;
  esac
])
DIFF_TROUBLE=$rcs_cv_status_diff_trouble
AC_MSG_RESULT($DIFF_TROUBLE)

# Set DIFFFLAGS to the options of the `diff' program to be run.
# Use -an if possible, -n otherwise.
AC_SUBST(DIFFFLAGS)
AC_MSG_CHECKING([diff options for RCS])
AC_CACHE_VAL(rcs_cv_options_diff, [
  rcs_cv_options_diff=$DIFFFLAGS
  case $rcs_cv_options_diff in
  '')
    rcs_cv_options_diff=-n
    $DIFF -an conftest0 conftest1 >conftestout 2>conftestout2
    case $? in
    1)
      if cmp -s conftestok conftestout && test ! -s conftestout2
      then rcs_cv_options_diff=-an
      fi
      ;;
    esac
    ;;
  esac
])
DIFFFLAGS=$rcs_cv_options_diff
AC_MSG_RESULT($DIFFFLAGS)

# Set DIFF_L to 1 if diff understands the L option, 0 otherwise.
AC_SUBST(DIFF_L)
AC_MSG_CHECKING([diff -L])
AC_CACHE_VAL(rcs_cv_options_diff_l, [
  rcs_cv_options_diff_l=$DIFF_L
  case $rcs_cv_options_diff_l in
  '')
    rcs_cv_options_diff_l=0
    $DIFF -c -L 0 -L 1 conftest0 conftest1 >conftestout 2>/dev/null
    case $? in
    1)
      if cmp -s - conftestout <<'EOF'
*** 0
--- 1
***************
*** 1 ****
! 0
--- 1 ----
! 1
EOF
      then rcs_cv_options_diff_l=1
      fi
      ;;
    esac
    ;;
  esac
])
DIFF_L=$rcs_cv_options_diff_l
case $DIFF_L in
1) AC_MSG_RESULT(yes);;
*) AC_MSG_RESULT(no);;
esac

# Set DIFF3 to the name of the diff3 program.
# In some systems (e.g. BSD/OS 2.0), diffutils diff3 lives in /usr/libexec.
diff3PATH=$PATH:/usr/libexec
AC_SUBST(DIFF3)
AC_MSG_CHECKING([diff3 -m])
AC_CACHE_VAL(rcs_cv_prog_diff3_bin, [
  rcs_cv_prog_diff3_bin=$DIFF3
  case $rcs_cv_prog_diff3_bin in
  '')
    PATH=$diff3PATH sh -c "exec diff3 -E -m -L 0 -L 1 -L 2 conftest0 conftest1 /dev/null" >conftestout 2>/dev/null
    case $? in
    1)
      if cmp -s - conftestout <<'EOF'
<<<<<<< 0
0
=======
>>>>>>> 2
EOF
      then rcs_cv_prog_diff3_bin=diff3
      fi
      ;;
    esac
  ;;
  esac
])
case $rcs_cv_prog_diff3_bin in
?*)
  AC_MSG_RESULT(yes)
  ac_save_path=$PATH
  PATH=$diff3PATH
  AC_PATH_PROG(DIFF3, $rcs_cv_prog_diff3_bin, $rcs_cv_prog_diff3_bin)
  PATH=$ac_save_path
  ;;
'')
  AC_MSG_RESULT(no)
  AC_MSG_CHECKING([diff3 library program])
  dnl We can't use AC_PATH_PROG since we don't want to inspect /bin,
  dnl and AC_PATH_PROG uses `test'.
  AC_CACHE_VAL(rcs_cv_path_diff3_lib, [
    $DIFF conftest0 conftest1 >conftest01
    $DIFF /dev/null conftest1 >conftestn1
    for i in /usr/*lib*/*diff3*; do
      sh -c "exec $i -E conftest01 conftestn1 conftest0 /dev/null conftest1" >conftestout 2>/dev/null
      # The exit status is arbitrary!  Test the output a bit.
      if
	grep '^<<* *conftest0$' conftestout >/dev/null 2>&1 &&
	grep '^>>* *conftest1$' conftestout >/dev/null 2>&1 &&
	grep '^0a$' conftestout >/dev/null 2>&1
      then
	rcs_cv_path_diff3_lib=$i
	break
      fi
    done
  ])
  DIFF3=$rcs_cv_path_diff3_lib
  case $DIFF3 in
  '') AC_MSG_ERROR(cannot find a working diff3 library program);;
  ?*) AC_MSG_RESULT($DIFF3);;
  esac
  ;;
esac

AC_SUBST(DIFF3_BIN)
case $DIFF3_BIN in
'')
  case $rcs_cv_prog_diff3_bin in
  '') DIFF3_BIN=0;;
  ?*) DIFF3_BIN=1;;
  esac
  ;;
esac

# Clean up simple `diff' test.
rm -f conftest*

AC_PATH_PROG(ED, ed, ed)

ac_save_path=$PATH
PATH=/usr/bin:/bin:/usr/sbin:/sbin:/usr/lib:$PATH
AC_PATH_PROGS(SENDMAIL, sendmail mail mailx)
PATH=$ac_save_path
case $SENDMAIL in
?*) SENDMAIL=\"$SENDMAIL\"
esac

# Use the GNU pic -n option if available; it avoids GNU extensions,
# which is need for proper operation to generate a portable man page.
# Similarly, if using traditional pic, use its -D option.
AC_CHECK_PROGS(PIC, "pic -n" "gpic -n" "pic -D" "pic", pic)

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AS_IF([test 1 = `expr "$ac_pwd" : '/*'`],[woe=no],[woe=yes])
RCS_CBOOL([WOE],[yes],[woe],[Are we trying to "support" MS-DOS?])

if test "x$exec_prefix" = xNONE
then
  CO="$ac_default_prefix/bin/co"
else
  eval CO="${bindir}/co"
fi
AC_DEFINE_UNQUOTED([CO], ["${CO}"],
	[name of 'co' program])

AC_DEFINE_UNQUOTED([DIFF], ["${DIFF}"],
	[name of 'diff' program])

AC_DEFINE_UNQUOTED([DIFF3], ["${DIFF3}"],
	[name of 'diff3' program])

AC_DEFINE_UNQUOTED([DIFF3_BIN], [${DIFF3_BIN}],
	[Is diff3 user-visible (not the /usr/lib auxiliary)?])

AC_DEFINE_UNQUOTED([DIFFFLAGS], ["${DIFFFLAGS}"],
	[Make diff output suitable for RCS.])

AC_DEFINE_UNQUOTED([DIFF_L], [${DIFF_L}],
	[Make diff output suitable for RCS. ])

AC_DEFINE([DIFF_SUCCESS], 0,
	[DIFF status if no differences are found])

AC_DEFINE([DIFF_FAILURE], 1,
	[DIFF status if differences are found])

AC_DEFINE([DIFF_TROUBLE], 2,
	[DIFF status if trouble])

AC_DEFINE_UNQUOTED([ED], ["${ED}"],
	[name of 'ed' program (used only if !DIFF3_BIN)])

AC_DEFINE_UNQUOTED([MERGE], ["${MERGE}merge"],
	[name of 'merge' program])

AC_CACHE_CHECK([configuring fread_type, freadarg_type],[rcs_cv_fread_type],[
rcs_cv_fread_type=
rcs_cv_freadarg_type=
AC_LANG_CONFTEST([
AC_LANG_PROGRAM([[
#include <stdio.h>
#define CHAR1 '#' /* the first character in this file */
#if !defined(fread) && declare_fread
        fread_type fread (void*,freadarg_type,freadarg_type,FILE*);
#endif
]],[[
        char b;
        return !(
                fread(&b, (freadarg_type)1, (freadarg_type)1, stdin) == 1  &&
                b==CHAR1
        );
]])
])
rm -f conftest.txt
echo '#' > conftest.txt
for declare_fread in 1 0
do
        for fread_type in ssize_t size_t int unsigned
        do
                for freadarg_type in size_t ssize_t unsigned int
                do
                        rm -f conftest$ac_exeext || exit
                        (
                                $CC -o conftest$ac_exeext \
                                        -Ddeclare_fread=$declare_fread \
                                        -Dfreadarg_type=$freadarg_type \
                                        -Dfread_type=$fread_type \
                                        conftest.$ac_ext 2>conftest.err &&
                                ./conftest$ac_exeext < conftest.txt
                        ) >&2 && break
                done && break
        done && break
done || {
        echo >&6 $0: cannot deduce fread types
        exit 1
}

rcs_cv_fread_type=$fread_type
rcs_cv_freadarg_type=$freadarg_type
])

AC_SUBST(FREAD_TYPE)
AC_DEFINE_UNQUOTED([FREAD_TYPE], [$rcs_cv_fread_type],
	[type returned by fread() and fwrite()])

AC_SUBST(FREADARG_TYPE)
AC_DEFINE_UNQUOTED([FREADARG_TYPE], [$rcs_cv_freadarg_type],
	[type of fread() size arguments])


AC_CACHE_CHECK([configuring malloc_type], [rcs_cv_malloc_type],[
rcs_cv_malloc_type=char
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
typedef void *malloc_type;
#ifndef malloc
        malloc_type malloc();
#endif
static malloc_type identity (malloc_type);
static malloc_type identity(x) malloc_type x; { return x; }
]],[[ return !identity(malloc(1)); ]]
)], [rcs_cv_malloc_type=void])
])

AC_SUBST(MALLOC_TYPE)
AC_DEFINE_UNQUOTED([MALLOC_TYPE], [$rcs_cv_malloc_type],
	[type returned by malloc()])

AC_CACHE_CHECK([configuring SLASHSLASH_IS_SLASH],[rcs_cv_slashslash_is_slash],[
rcs_cv_slashslash_is_slash=0
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <sys/stat.h>
#define same_file(s,t,d) ((s).st_ino==(t).st_ino && (s).st_dev==(t).st_dev)
static struct stat s, ss;
static char f[3];
]],[[
        f[0] = SLASH; if (stat(f, &s ) != 0) return 1;
        f[1] = SLASH; if (stat(f, &ss) != 0) return 1;
        return !same_file(s, ss, 0);
]])],[rcs_cv_slashslash_is_slash=1])
])

AC_SUBST(SLASHSLASH_IS_SLASH)
AC_DEFINE_UNQUOTED([SLASHSLASH_IS_SLASH], [${rcs_cv_slashslash_is_slash}],
	[Are // and / the same directory?])


AC_CACHE_CHECK([configuring has_rename, bad_a_rename, bad_b_rename],[rcs_cv_bad_a_rename],[
rcs_cv_bad_a_rename=0
rcs_cv_bad_b_rename=0
AC_LANG_CONFTEST([
AC_LANG_PROGRAM([[
#include <stdio.h>
]],[[
return rename("a.a","a.b") != 0;
]])])

_AC_DO_VAR(ac_link) || exit
# prepare
rm -f a.b
echo a > a.a

if (./conftest$ac_exeext && test -f a.b) >&2
then
        h=1
        rm -f a.a a.b &&
        echo a > a.a && chmod -w a.a || exit
        if ./conftest$ac_exeext && test ! -f a.a && test -f a.b
        then a=0
        else a=1
        fi
        rm -f a.a a.b &&
        echo a > a.a && echo b > a.b && chmod -w a.b || exit
        if ./conftest$ac_exeext && test ! -f a.a && test -f a.b
        then b=0
        else b=1
        fi
        rm -f a.a a.b || exit
else h=0 a=0 b=0
fi
rcs_cv_bad_a_rename=$a
rcs_cv_bad_b_rename=$a
])

AC_SUBST(BAD_A_RENAME)
AC_DEFINE_UNQUOTED([BAD_A_RENAME], [${rcs_cv_bad_a_rename}],
	[Does rename(A,B) fail if A is unwritable?])

AC_SUBST(BAD_A_RENAME)
AC_DEFINE_UNQUOTED([BAD_B_RENAME], [${rcs_cv_bad_b_rename}],
	[Does rename(A,B) fail if B is unwritable?])

# FIXME
AC_SUBST(BAD_NFS_RENAME)
AC_DEFINE_UNQUOTED([BAD_NFS_RENAME], [0],
	[Can rename(A,B) falsely report success?])

AC_CACHE_CHECK([configuring bad_chmod_close],[rcs_cv_bad_chmod_close],[
rcs_cv_bad_chmod_close=0
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#ifndef O_RDONLY
#       define O_RDONLY 0
#endif
]],[[
        int f;
        return (
                (f = open("a.c", O_RDONLY)) < 0 ||
                chmod("a.c", 0) != 0 ||
                close(f) != 0
        );
]])], [rcs_cv_bad_chmod_close=1])
])

AC_SUBST(BAD_CHMOD_CLOSE)
AC_DEFINE_UNQUOTED([BAD_CHMOD_CLOSE], [${rcs_cv_bad_chmod_close}],
	[Can chmod() close file descriptors?])

AC_CACHE_CHECK([configuring ALL_ABSOLUTE, DIFF_ABSOLUTE],[rcs_cv_all_absolute],[
rcs_cv_diff_absolute=0
rcs_cv_all_absolute=0
AC_LANG_CONFTEST([AC_LANG_SOURCE([[
]AC_INCLUDES_DEFAULT[
#if !WOE
#define SLASHes '/'
#define ROOTPATH(p)  (isSLASH ((p)[0]))
#else
#define SLASHes '\\\\': case '/': case ':'
#define ROOTPATH(p)  (isSLASH((p)[0]) || (p)[0] && (p)[1]==':')
#endif

#ifndef isSLASH
static int
isSLASH(c) int c; {
        switch (c) { case SLASHes: return 1; } return 0;
}
#endif
int main(int argc, char**argv) {
        return 1 < argc && !ROOTPATH(argv[1]);
}
]])])
_AC_DO_VAR(ac_link) && ./conftest$ac_exeext || exit
a=1
# check ALL_ABSOLUTE
for i in "$DIFF" "$DIFF3" "$ED" "$SENDMAIL"
do
        case $i in
        \"*\") i=`expr "$i" : '"\(.*\)"'`
        esac
        case $i in
        ?*) ./conftest$ac_exeext "$i" || { a=0; break; }
        esac
done
rcs_cv_all_absolute=$a

# check DIFF_ABSOLUTE
if ./conftest$ac_exeext "$DIFF"
then a=1
else a=0
fi
rcs_cv_diff_absolute=$a
])

AC_SUBST(ALL_ABSOLUTE)
AC_DEFINE_UNQUOTED([ALL_ABSOLUTE], [${rcs_cv_all_absolute}],
	[Do all subprograms satisfy ROOTPATH?])

AC_SUBST(DIFF_ABSOLUTE)
AC_DEFINE_UNQUOTED([DIFF_ABSOLUTE], [${rcs_cv_diff_absolute}],
	[Is ROOTPATH(DIFF) true?])


AC_SUBST(LARGE_MEMORY)
AC_MSG_CHECKING([configuring large_memory])
AC_CACHE_VAL(rcs_cv_large_memory, [
  rcs_cv_large_memory=0
  case "$ac_cv_func_map_fd$ac_cv_func_mmap_fixed_mapped" in
  *1*) rcs_cv_large_memory=1;;
  *) rcs_cv_large_memory=0
  esac
])
LARGE_MEMORY=$rcs_cv_large_memory
AC_MSG_RESULT($LARGE_MEMORY)

AC_DEFINE_UNQUOTED([LARGE_MEMORY], [${LARGE_MEMORY}],
	[Can main memory hold entire RCS files?])

AC_CACHE_CHECK([configuring has_fflush_input],[rcs_cv_have_fflush_input],[
rcs_cv_have_fflush_input=0
AC_LANG_CONFTEST([
AC_LANG_PROGRAM([[
#include "stdio.h"
#ifndef SEEK_SET
#define SEEK_SET 0
#endif
#ifndef SEEK_CUR
#define SEEK_CUR 1
#endif
#ifndef STDIN_FILENO
#define STDIN_FILENO 0
#endif
]],[[
        return (
                getchar() == EOF
                || fseek(stdin, 0L, SEEK_SET) != 0
                || fflush(stdin) != 0
                || lseek(STDIN_FILENO, (off_t)0, SEEK_CUR) != 0
        );
]])])
_AC_DO_VAR(ac_link) || exit
if ./conftest$ac_exeext < conftest.$ac_ext
then
rcs_cv_have_fflush_input=1
fi
])

AC_SUBST(HAVE_FFLUSH_INPUT)
AC_DEFINE_UNQUOTED([HAVE_FFLUSH_INPUT], [$rcs_cv_have_fflush_input],
	[Does fflush() work on input files?])

# some consts
AC_SUBST(USE_NFS)
AC_DEFINE_UNQUOTED([USE_NFS], [1],
	[Might NFS be used?])

AC_SUBST(TZ_MUST_BE_SET)
AC_DEFINE_UNQUOTED([TZ_MUST_BE_SET], [0],
	[Must TZ be set for gmtime() to work?])

AC_SUBST(RCS_SHELL)
AC_DEFINE_UNQUOTED([RCS_SHELL], ["/bin/sh"],
	[shell to run RCS subprograms])

AC_SUBST(NEEDS_GETABSNAME)
AC_DEFINE_UNQUOTED([NEEDS_GETABSNAME], [0],
	[Must we define getabsname?])

AC_SUBST(COMPAT2)
AC_DEFINE_UNQUOTED([COMPAT2], [0],
	[Are version 2 files supported?])


AC_MSG_CHECKING([gcc noreturn attribute])
gcc_noreturn=yes
AC_TRY_COMPILE([
	__attribute__ ((noreturn)) void f();
        void f(){ return 1;}
	], [],
	AC_DEFINE([HAVE_GCC_NORETURN],1,
		[Define if gcc supports noreturn attribute]),
	gcc_noreturn=no)
AC_MSG_RESULT($gcc_noreturn)
#

AC_CACHE_CHECK([if `fopen (F, "w+")' truncates],[rcs_cv_fopen_truncates],[
  echo nonempty > conftest.data
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <stdio.h>
]],[[
  return ! fopen ("conftest.data", "w+");
]])
],[res=yes],[res=no],[res=no])
  test yes = $res && test ! -s conftest.data || res=no
  rcs_cv_fopen_truncates=$res
])
RCS_CBOOL([BAD_FOPEN_WPLUS],[no],[rcs_cv_fopen_truncates],
  [Does `fopen (F, "w+")' fail to truncate?])

AC_CACHE_CHECK([if `write' after `creat (F, 0)' works],[rcs_cv_wac_ok],[
  rm -f conftest.data
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <unistd.h>
#include <sys/types.h>
#include <sys/stat.h>
#include <fcntl.h>
#if defined O_CREAT && defined O_WRONLY
# define creat0(f) open(f, O_CREAT|O_WRONLY, 0)
#else
# define creat0(f) creat(f, 0)
#endif
char buf[17000];
]],[[
  int f;
  return (0 > (f = creat0 ("conftest.data"))
          || sizeof (buf) != write (f, buf, sizeof (buf))
          || 0 > close (f));
]])
],[res=yes],[res=no],[res=no])
  AS_IF([test yes = "$res" \
         && test -f conftest.data \
         && test ! -w conftest.data],,
        [res=no])
  rcs_cv_wac_ok=$res
])
RCS_CBOOL([BAD_CREAT0],[no],[rcs_cv_wac_ok],
  [Does `write' after `creat (F, 0)' fail in the wrong way?])

AC_CACHE_CHECK([if `unlink' works on unwritable files],[rcs_cv_unlink_mw_ok],[
  touch conftest.data
  chmod -w conftest.data
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <unistd.h>
]],[
  return 0 > unlink ("conftest.data");
])
],[res=yes],[res=no],[res=no])
  test yes = $res && test -f conftest.data && res=no
  rcs_cv_unlink_mw_ok=$res
])
RCS_CBOOL([BAD_UNLINK],[no],[rcs_cv_unlink_mw_ok],
  [Does `unlink' fail on unwritable files?])

AC_CACHE_CHECK([if `wait' can handle ignored SIGCHLD],[rcs_cv_tolerant_wait],[
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <sys/types.h>
#include <errno.h>
#include <signal.h>
#include <sys/wait.h>
#include <unistd.h>
]],[
  signal (SIGCHLD, SIG_IGN);
#ifdef HAVE_WORKING_FORK
  {
    int status;
    pid_t p = fork ();
    if (p < 0)
      {
        perror ("fork");
        return 2;
      }
    if (!p)
      _exit (0);
    while (wait (&status) != p)
      {
        if (ECHILD == errno)
          return 1;
        if (EINTR != errno)
          {
            perror ("wait");
            return 2;
          }
      }
  }
#else
  if (0 > system ("true"))
    return 1;
#endif
])dnl
],[res=yes],[res=no],[res=no])
  rcs_cv_tolerant_wait=$res
])
RCS_CBOOL([BAD_WAIT_IF_SIGCHLD_IGNORED],[no],[rcs_cv_tolerant_wait],
  [Does ignoring SIGCHLD break `wait'?])

AC_CACHE_CHECK([configuring READLINK_ISREG_ERRNO],[rcs_cv_readlink_isreg_errno],[
rm -f a.sym* || exit
ln -s a.sym1 a.sym2
rcs_cv_readlink_isreg_errno='EINVAL'
AC_LANG_CONFTEST([
AC_LANG_PROGRAM([[
#include "confdefs.h"
#include <stdio.h>
#include <errno.h>
static char b[7];
]],[[
        if (readlink("a.sym2",b,7) == 6  &&  strcmp(b,"a.sym1") == 0  &&
                readlink("conftest.c",b,7) == -1  &&  errno != ENOENT
        ) {
                if (errno == EINVAL)
                        printf("EINVAL\n");
                else
                        printf("%d\n", errno);
                return ferror(stdout) || fclose(stdout)!=0;
        }
        return 1;
]])])
_AC_DO_VAR(ac_link) || exit
if rcs_cv_readlink_isreg_errno=`./conftest$ac_exeext`
then res=1
else res=0
fi
rm -f a.sym*
])
AC_DEFINE_UNQUOTED([READLINK_ISREG_ERRNO], [${rcs_cv_readlink_isreg_errno}],
	[errno after readlink on regular file])

#AC_CACHE_CHECK([configuring LONG_MAX],[rcs_cv_ulong_max],[
#rcs_cv_ulong_max=$(printf '#include <limits.h>\nlong_max:LONG_MAX\n' |gcc -E - | grep long_max | cut -d: -f2)
#])

AC_CACHE_CHECK([configuring struct utimbuf],[rcs_cv_utimbuf],[
rcs_cv_utimbuf=0
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <utime.h>
static struct utimbuf s;
]],[[
s.actime = s.modtime = 1;
return utime("conftest.c", &s) != 0;
]])], [rcs_cv_utimbuf=1])
])

AC_SUBST(HAVE_STRUCT_UTIMBUF)
AC_DEFINE_UNQUOTED([HAVE_STRUCT_UTIMBUF], [${rcs_cv_utimbuf}],
	[Does struct utimbuf work?])

AC_CACHE_CHECK([configuring HAVE_PRINTF_DOT],[rcs_cv_printf_dot],[
rcs_cv_printf_dot=0
AC_LANG_CONFTEST([
AC_LANG_PROGRAM([[
#include <stdio.h>
]],[[
printf("%.2d", 1);
return ferror(stdout) || fclose(stdout) != 0;
]])])
_AC_DO_VAR(ac_link) || exit
res=`./conftest$ac_exeext` || exit
case $r in
01)     rcs_cv_printf_dot=1;;
*)      rcs_cv_printf_dot=0
esac
])

AC_CONFIG_FILES([Makefile man/Makefile src/Makefile])

AC_OUTPUT
